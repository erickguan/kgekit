plugins {
    id 'java'
    id 'application'
}

import java.util.regex.Matcher

sourceCompatibility = JavaVersion.VERSION_1_10

ext.moduleName = 'me.erickguan.kgekit.cli'

dependencies {
    implementation project(':triple-transform')
    implementation 'org.eclipse.rdf4j:rdf4j-rio-api:2.4.0-M3'
}


mainClassName = "$moduleName/me.erickguan.kgekit.cli.Driver" // <1>

// tag::startScripts[]
startScripts {
    inputs.property("moduleName", moduleName)
    doFirst {
        classpath = files()
        defaultJvmOpts = [
                '--module-path', 'APP_HOME_LIBS',  // <1>
                '--module', mainClassName
        ]
    }
    doLast{
        def bashFile = new File(outputDir, applicationName)
        String bashContent = bashFile.text
        bashFile.text = bashContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('$APP_HOME/lib'))

        def batFile = new File(outputDir, applicationName + ".bat")
        String batContent = batFile.text
        batFile.text = batContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('%APP_HOME%\\lib'))
    }
}

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module', mainClassName // <2>
        ]
        classpath = files()
    }
}

jar {
    manifest {
        attributes('Main-Class': 'me.erickguan.kgekit.cli.Driver')
    }
}